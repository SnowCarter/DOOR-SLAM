include "buzz_includes/buzz_utils/vec2.bzz"
include "buzz_includes/buzz_utils/string.bzz"
include "config/ros/buzz_script_parameters.bzz"
include "config/ros/slam_parameters.bzz"
include "config/ros/slam_ros_parameters.bzz"
include "buzz_includes/ros/global_variables.bzz"
include "buzz_includes/ros/pose_graph_optimization_trigger.bzz"
include "buzz_includes/pose_graph_optimization.bzz"
include "buzz_includes/schedule.bzz"

# Executed at init time
function init() {
  # Initialization of random number generator. The controller function use time+id as the seed.
  srand(id)
  # Load parameters
  load_slam_ros_parameters()
  load_slam_parameters()
}

function execute_step() {
  if (current_step_ == 0) {
    # Init optimizer
    init_optimizer(OPTIMIZER_PERIOD)
  }
  
  if (current_step_ == 5) {
    print(current_step_, " : Communication is OK")
  }
  # Optimizer clock tick to trigger the state machine
  optimizer_tick()

  # Send state
  send_optimizer_state_to_neighbors()

  # State machine
  if (optimizer_state() == 0) { # Not optimizing
    # Reset flags
    has_received_rotation_estimates = 0
    has_received_pose_estimates = 0
    # Communication
    # Note that incoming messages (listen) are executed always first (after sensing), 
    # see Section II of "Buzz: An Extensible Programming Language for Self-Organizing Heterogeneous Robot Swarms"
    send_start_optimization_flag()
  } else if (optimizer_state() == 1) {  # Start optimizing
    print(current_step_, "Start optimization");
    start_optimization()
  } else if (optimizer_state() == 3) {  # Rotation Estimation
    # Send estimates to neighbors
    send_rotation_estimates_to_neighbors()
    # Send finished flag if stopping conditions are met
    send_rotation_estimation_finished_flag_to_neighbors()
  } else if (optimizer_state() == 5) {  # PoseEstimation
    # Send estimates to neighbors
    send_pose_estimates_to_neighbors()
    # Send finished flag if stopping conditions are met
    send_pose_estimation_finished_flag_to_neighbors()
  } else if (optimizer_state() == 6) {  # End optimization
    print(current_step_, "End optimization");
    number_of_steps_at_optimization_end = current_step_
    number_of_steps_with_neighbors = 0
    neighbors_in_separator_range.foreach( function(neighbor_in_range_id, neighbor_in_range_data) {
        last_optimization_neighbor = neighbor_in_range_id
      }
    )
  } else {
  }
  # Increment number of steps
  current_step_ = current_step_ + 1
}
 
# Executed every time step
function step() {
  # The listener functions are always on
  receive_optimizer_state_from_neighbors()
  listen_for_start_optimization_flag()
  receive_rotation_estimation_finished_flag_from_neighbors()
  receive_and_update_rotation_estimates_from_neighbors()
  receive_pose_estimation_finished_flag_from_neighbors()
  receive_and_update_pose_estimates_from_neighbors()
  # Listen for scheduler
  neighbors.listen("start_step", 
    function(vid, value, rid) {   
      start_step_received = 1
    }
  )
  if (id == 0) {
    start_step = schedule(NUMBER_OF_ROBOTS-1)
    if (start_step == 1) {
      execute_step()
    }
  } else {
    if (start_step_received) {
      execute_step()
      neighbors.broadcast("end_step", 1)
      start_step_received = 0
    }
  }
}
 
# Execute at exit
function destroy() {
}
